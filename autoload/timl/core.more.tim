(in-ns 'timl#core)
(defmacro ns (ns)
  (list
    'do
    (if (not (f:exists (string "g:" (timl#munge (string ns "#" "*uses*")))))
      `(setq ,(f:timl#symbol (string ns "#*uses*")) (list 'timl#core)))
    `(in-ns ',ns)))

(defun pr-str (...) (f:join (map timl#pr-str ...)))
(defun pr (...) (echo (apply pr-str ...)))

(defmacro loop (bindings ...)
 `((lambda ,(map first bindings) ,@...) ,@(map last bindings)))
(defun 1+ (n) (+ n 1))
(defun 1- (n) (- n 1))
(defun reverse (xs) (f:reverse (f:copy xs)))
(defun not (x) (if x 0 1))
(defun second (xs) (get xs 1 nil))
(defun last (xs) (get xs -1 nil))
(defun butlast (xs) (sublist xs 0 -2))
(defun get-in (m ks) (reduce get m ks))

(defun read-string (string) (timl#reader#read-string string))
(defun eval (form) (timl#eval form))

(defun constantly (x) (lambda (...) x))
(defun partial (f ...)
  (let ((args ...))
    (lambda (...) (apply f (append args ...)))))
(defun compose (f ...)
  (if-let (fns ...)
    (lambda (...) (f (apply (apply compose fns) ...)))
    f))

(defun sorter (>)
  (lambda (x y)
    (if (> x y)
      1
      (if (> y x)
        -1
        0))))

(defun sort (xs ...)
  (if ...
    (f:sort (f:copy xs) (sorter (first ...)) {})
    (f:sort (f:copy xs))))

(defun reductions (f init xs)
  (cons init (if xs
    (reductions f (f init (first xs)) (rest xs)))))

(defmacro or (x ...)
  (if ...
    `(let ((or# ,x)) (if or# or# (or ,@...)))
    x))

(defmacro and (x ...)
  (if ...
    `(let ((and# ,x)) (if and# (and ,@...) and#))
    x))

(defmacro when (test ...)
  `(if ,test (do ,@...)))

(defmacro if-let (binding then ...)
  (let ((form (first binding))
        (tst (last binding)))
    `(let ((temp# ,tst))
      (if temp#
        (let ((,form temp#)) ,then) ,@...))))

(defmacro -> (x ...)
  (if ...
    (let ((form (first ...))
          (more (rest ...)))
      (if more
        `(-> (-> ,x ,form) ,@more)
        (if (list? form)
          `(,(first form) ,x ,@(rest form))
          (list form x))))
    x))

(defmacro capture (...)
  `(do
     (try
       (:redir "=> g:timl_core_captured")
       ,@...
       (finally (:redir "END")))
     (f:substitute g:timl_core_captured "^\n" "" "")))

(defmacro include-guard (var)
  (let ((str (timl#munge (string var))))
   `(if (f:exists ,str)
     (:finish)
     (:let ,str " = 1"))))

(defun build-option-string (...)
  (sublist
    (loop ((s "")
           (x (first ...))
           (more (rest ...)))
      (if (nil? x)
        s
        (recur
          (if (symbol? x) (string s " " x) (string s (f:fnameescape (string x))))
          (first more) (rest more))
        )) 1 -1))

(defun extract-option-names (...)
  (map (lambda (x) (f:matchstr (string x) "\\%(inv\\|no\\)\\=\\zs\\w\\+")) (filter symbol? ...)))

(defun extract-option-restore (...)
  (if-let (options (apply extract-option-names ...))
    (string "setlocal " (f:join options "< ") "<")))

(defmacro setlocal (...)
  `(:setlocal ,(apply build-option-string ...)))

(defmacro setglobal (...)
  `(:setlocal ,(apply build-option-string ...)))
